plugins {
	id 'fabric-loom' version '0.8-SNAPSHOT'
	id 'maven-publish'
}

repositories {
	maven {
		name = "JitPack"
		url = "https://jitpack.io"
	}
	maven {
		name = "TerraformersMC"
		url = "https://maven.terraformersmc.com/"
	}
	maven {
		name = 'Ladysnake Mods'
		url = 'https://ladysnake.jfrog.io/artifactory/mods'
		content {
			includeGroup 'io.github.ladysnake'
			includeGroupByRegex 'io\\.github\\.onyxstudios.*'
		}
	}
}

loom {
    accessWidener = file("src/main/resources/hudtweaks.accesswidener")
}

group = project.maven_group

allprojects {
	apply plugin: 'fabric-loom'

	tasks.withType(JavaCompile).configureEach {
		// ensure that the encoding is set to UTF-8, no matter what the system default is
		// this fixes some edge cases with special characters not displaying correctly
		// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
		// If Javadoc is generated, this must be specified in that task too.
		it.options.encoding = "UTF-8"

		// Minecraft 1.17 (21w19a) upwards uses Java 16.
		it.options.release = 16
	}
	
	archivesBaseName = project.name
	version = project.mod_version
	
	dependencies {
		minecraft "com.mojang:minecraft:${project.minecraft_version}"
		mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
		modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
	}

	configurations {
		dev
	}

	jar {
		archiveClassifier.set "dev"
	}

	loom {
		shareCaches = true
	}
	
	java {
		withSourcesJar()
	}

	jar {
		from "${rootDir}/LICENSE.txt"
	}

	afterEvaluate {
		processResources {
			inputs.property "version", project.version

			filesMatching("fabric.mod.json") {
				expand "version": project.version
			}
		}
	}
}

dependencies {
	afterEvaluate {
		implementation project(":HUDTweaks-apihelper")
		include project(":HUDTweaks-apihelper")
	}

	include "net.fabricmc.fabric-api:fabric-resource-loader-v0:${project.resource_loader_version}"

	modImplementation "io.github.ladysnake:satin:${project.satin_version}"
	include "io.github.ladysnake:satin:${project.satin_version}"

	modCompileOnly "com.terraformersmc:modmenu:${project.modmenu_version}"
	modRuntime "com.terraformersmc:modmenu:${project.modmenu_version}"
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			// add all the jars that should be included when publishing to maven
			artifact(remapJar) {
				builtBy remapJar
			}
			artifact(sourcesJar) {
				builtBy remapSourcesJar
			}
		}
	}
	
	// Select the repositories you want to publish to
	// To publish to maven local, no extra repositories are necessary. Just use the task `publishToMavenLocal`.
	repositories {
		// See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
	}
}
